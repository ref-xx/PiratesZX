0 REM PIRATES OF THE SPANISH MAIN!!
4 POKE52,142:POKE56,142:CLR:T$="":DIMNA$(30),N1(30),N2(30),N3(30)
5 GOSUB40:GOSUB19000:GOTO50
7 VZ=VA+2*V:POKEVZ,FNLB(ZV):POKEVZ+1,ZV/KP:RETURN
8 VZ=VA+2*V:ZV=PEEK(VZ)+KP*PEEK(VZ+1):RETURN
10 POKEA0,FNLB(F*KP):POKEA1,F:POKEA2,FNLB(T*KP):POKEA3,T:POKEA4,FNLB(L*KP)
11 POKEA5,L:SYSM64K:RETURN
12 POKEA0,F:POKEA2,FNLB(T*KP):POKEA3,T:POKEA4,FNLB(L*KP)
13 POKEA5,L:SYSSB:RETURN
20 OPEN15,8,15,"I":PRINT#15,"S0:LIFE":CLOSE15:SAVE"LIFE",8:END
30 POKEA1,Y:POKEA3,DY:POKEA4,CO:POKEA0,X:POKEA2,DX:SYSWN:RETURN
40 POKE792,193:POKE808,234:POKE56333,129:RETURN
41 POKE56333,127:POKE808,237:POKE792,71:RETURN
50 VB=150*KP:WN=153.5*KP:CTY=143*KP:CLN=24:NC=PEEK(CTY+1023)-3:PRS=148*KP
55 PTY=PRS+32:MP=155.5*KP:TR=142*KP:SO=158*KP:LG=147*KP:DTA=6*KP:SG=-1
56 GOSUB12000:NA$="":I=0
57 Z=PEEK(PRS+80+I):NA$=NA$+CHR$(Z):I=I+1:IFZTHEN57
58 T$="THE FURTHER ADVENTUREÓOF THE PIRATE "+NA$+"º":GOSUB12500:X=5:Y=7
59 GOSUB8005
60 NN=-1
61 NN=NN+1:READNA$(NN),N1(NN),N2(NN),N3(NN):IFNN<>23THEN61
100 DATA KING&S ADVISOR,269,81,4
101 DATA GOVERNOR,233,67,3
102 DATA LT. GOVERNOR,254,63,0
103 DATA FLEET ADMIRAL,249,70,0
104 DATA RICH BANKER,253,71,0
105 DATA PLANTATION OWNER,246,62,3
106 DATA WEALTHY MERCHANT,269,70,1
107 DATA GENERAL,241,63,0
108 DATA SUGAR PLANTER,251,87,0
109 DATA MERCHANT CAPTAIN,270,72,4
110 DATA COUNCILMEMBER,251,62,0
111 DATA COLONEL,245,56,1
112 DATA SHOP OWNER,253,63,0
113 DATA MAJOR,237,74,4
114 DATA TAVERNKEEPER,260,64,2
115 DATA SAILING MASTER,252,72,5
116 DATA SERGEANT,251,73,1
117 DATA BARTENDER,236,65,3
118 DATA SAILOR,236,65,7
119 DATA FARM HAND,251,60,6
120 DATA ROGUE,219,72,5
121 DATA SCOUNDREL,263,75,5
122 DATA PAUPER,247,58,8
123 DATA BEGGAR,253,71,2
150 FORI=0TO5:RK(I)=0:NEXTI:IFPEEK(PRS+18)AND1THENGOSUB3000
190 FORI=0TO3:RK=RK+PEEK(PRS+13+I):NEXTI
195 AGE=25+PEEK(PRS-5):X=1:Y=0:YR=1560+20*PEEK(PRS+26)
197 HA=1+PEEK(PRS+20)/8-PEEK(PRS+22)/6+PEEK(PRS+6)/8
198 YR=PEEK(PRS+21)-PEEK(PRS+19):HA=HA*(25-YR)/20:IFHA<-1THENHA=-1
200 RK(0)=RK*(FNDP(PRS+2)+50*PEEK(PRS+5))*(FNEX(PRS+4)+8)/750000*HA
205 RK(1)=(FNDP(PRS+2)+150*PEEK(PRS+5))/4000*HA
210 RK(2)=FNDP(PRS+2)/2000*HA
215 RK(3)=(RK+2*FNEX(PRS+4))/5
220 RK(4)=FNEX(PRS+4)*HA/3
225 IFHA<1THENRK(5)=(HA-1)*5
230 IFPEEK(PRS+18)AND1THENHA=2:GOTO240
235 GOSUB2000
240 Z=FNDP(PRS+2)+50*PEEK(PRS+5)+500*RK+200*FNEX(PRS+4)+1000*PEEK(PRS+20)
245 Z=Z+1000*PEEK(PR+6)-1000*PEEK(PR+22):PT=INT(Z/330*(25-YR)/20)
250 IFPT>0THENWH=19-INT(PT/5):IFWH<0THENWH=0
255 IFPT<=0THENWH=20-INT(PT/10):IFWH>23THENWH=23
260 GOSUB1000:GOSUB1400
280 IFPEEK(PRS+18)AND1THEN500
290 IFPEEK(PRS-3)=4OR(YR+PEEK(PRS+22))/(3-(PEEK(PRS+23)=4))>=4THEN400
300 T$="THE LURE OF THE SEA IS STRONÇAND YOU "
310 T$=T$+"MAY STILL HAVE ÁFEW GOOD YEARS LEFT IN YOU®WILL YOU..®"
320 T$=T$+" REMAIN IN RETIREMENT® RETURN TO THE PIRATE LIFE®"
330 GOSUB12500:X=3:Y=3:GOSUB8000:IFC<>5THEN400
335 POKEPRS+18,PEEK(PRS+18)OR4:BF=192*KP:F$="MAIN.CHR":GOSUB17800
340 F$="FIXMAP":GOSUB12900:V=22:ZV=NC:GOSUB7:V=41:ZV=224:GOSUB7:GOSUB12950
345 T$="GAME DISK SIDE 2":GOSUB900:IFI$<>"2"THEN300
350 F$="MAIN":GOTO21900
400 IFPT<0THEN500
405 T$="DO YOU WISH TO HAVE YOUÒEXPLOITS ENTERED INTO THÅPIRATE HALL OF FAME¿"
410 T$=T$+" YES® NO®":X=4:Y=11:GOSUB8000:IFC=4THEN500
420 T$="YOUR GAME SAVE DISK":GOSUB900:IFI$<>"3"THEN400
430 OPEN8,8,8,"FAME,S,R":FORI=0TO9:INPUT#8,NM$(I),CR$(I),PT(I):NEXTI:CLOSE8
440 ZZ=0:FORI=0TO9:IFPT<PT(I)THENZZ=I+1
441 NEXTI:IFZZ>8THEN450
445 FORI=8TOZZSTEP-1:NM$(I+1)=NM$(I):CR$(I+1)=CR$(I):PT(I+1)=PT(I):NEXTI
450 NM$(ZZ)=NA$:CR$(ZZ)=NA$(WH):PT(ZZ)=PT
460 PRINT"        PIRATE&S HALL OF FAME":PRINT
470 FORI=0TO9:PRINT"";:IFI=ZZTHENPRINT"";
471 PRINTI+1". "NM$(I);:POKE211,16:PRINTCR$(I);
475 POKE211,34:PRINTPT(I):NEXTI:PRINT""
480 OPEN15,8,15,"I":PRINT#15,"S0:FAME":CLOSE15:OPEN8,8,8,"FAME,S,W"
485 I$=CHR$(13):FORI=0TO9:PRINT#8,NM$(I);I$;CR$(I);I$;PT(I):NEXTI:CLOSE8
500 T$="DO YOU WISH TO PLAÙANOTHER GAME OF PIRATES¡ YES® NO®"
510 X=2:Y=17:GOSUB8000:IFC=3THEN600
515 GOSUB22000:IFI$="1"THEN530
520 T$="GAME DISK SIDE 1":GOSUB900:IFI$<>"1"THEN500
530 BF=192*KP:F$="MAIN.CHR":GOSUB17800
540 F$="PICK":GOTO21900
600 T$="THANK YOU, GOOD BYE®":X=8:Y=8:GOSUB12500:GOSUB8005
610 GOTO610
900 T$="INSERT "+T$+"£ THEN PRESS TRIGGER®":X=4:Y=20:GOSUB8000:GOSUB22000:RETURN
1000 T$="YOUR PIRATING DAYÓARE AT AN END. AÓ"
1005 T$=T$+"A RESULT OF YOUÒSEAFARING EXPLOITÓ"
1080 T$=T$+"YOU BECOME Á"+NA$(WH):IFPT<100THENT$=T$+"®":GOTO1990
1100 ON((PT-100)/20)+1GOTO1110,1111,1112,1113
1105 T$=T$+" (PRINCE)®":GOTO1990
1110 T$=T$+" (BARON)®":GOTO1990
1111 T$=T$+" (COUNT)®":GOTO1990
1112 T$=T$+" (MARQUIS)®":GOTO1990
1113 T$=T$+" (DUKE)®":GOTO1990
1400 Z=0:PRINT""
1425 PRINT"":MX=-999:FORI=0TONN
1435 IFI=WHTHENPRINT"";
1440 Z=INT(I/16):POKE214,I-8*Z+7:PRINT:POKE211,Z*18+2
1445 PRINTNA$(I)"";:NEXTI
1450 PRINT:PRINT,"PIRATE POINTS:"PT"/100";
1455 POKE53271,0:POKE53277,0:POKE53264,0:POKE53276,0
1460 POKESC+7,1:POKESC+6,10:POKESC+5,2:POKESC+4,0
1465 FORI=4TO7:POKESX+I+I,FNLB(N1(WH)):POKESY+I+I,N2(WH):POKESP+I,7-I:NEXTI
1470 POKE53264,0:IFN1(WH)>255THENPOKE53264,240
1475 F$="FACE"+STR$(N3(WH)):BF=192*KP:GOSUB17800
1480 X=20:Y=0:F$="CHR"+STR$(WH):C1=0:C2=10:GOSUB8100:POKE53269,240:Z=3:GOSUB8700
1500 Z=INT(RND(0)*6)*4:POKESP+7,Z:POKESP+6,Z+1:POKESP+5,Z+2:POKESP+4,Z+3
1510 FORI=1TO150/(INT(Z/6)+1)^2:IFPEEK(56320)AND16THENNEXTI:GOTO1500
1520 Z=-1:GOSUB8700:GOSUB12500:POKE53269,0:RETURN
1990 GOSUB12500:Y=0:X=0:GOSUB8005:RETURN
2000 REM FAME
2005 IFRK=0THENT$="YOU FAILED TO ACHIEVE ANÙRANK":GOTO2030
2010 T$="YOU ACHIEVED THE TITLE OÆ"
2015 K=0:FORI=0TO3:Z=PEEK(PRS+13+I):IFZ=0THEN2029
2016 IFKTHENT$=T$+" ANÄ"
2017 K=K+1:T$=T$+MID$("SPANISHENGLISHFRENCH DUTCH  ",I*7+1,7)+" "
2018 ONZGOTO2020,2021,2022,2023,2024,2025,2026,2027,2028
2020 T$=T$+"ENSIGN":GOTO2029
2021 T$=T$+"CAPTAIN":GOTO2029
2022 T$=T$+"MAJOR":GOTO2029
2023 T$=T$+"COLONEL":GOTO2029
2024 T$=T$+"ADMIRAL":GOTO2029
2025 T$=T$+"BARON":GOTO2029
2026 T$=T$+"COUNT":GOTO2029
2027 T$=T$+"MARQUIS":GOTO2029
2028 T$=T$+"DUKE":GOTO2029
2029 NEXTI
2030 T$=T$+"; MAKING YOÕ"
2035 ONRK/2.5+1GOTO2040,2041,2042,2043,2044,2045,2046
2039 T$=T$+"AN EXTREMELY IMPORTANT":GOTO2050
2040 T$=T$+"AN UNDISTINGUISHED":GOTO2050
2041 T$=T$+"A LITTLE-KNOWN":GOTO2050
2042 T$=T$+"AN AVERAGE":GOTO2050
2043 T$=T$+"A CREDITABLE":GOTO2050
2044 T$=T$+"A WELL RESPECTED":GOTO2050
2045 T$=T$+"A GREATLY ADMIRED":GOTO2050
2046 T$=T$+"A VERY IMPORTANT":GOTO2050
2050 T$=T$+" MEMBEÒOF YOUR COMMUNITY®"
2090 GOSUB12500:X=1:Y=1:GOSUB8000
2100 Z=FNDP(PRS+2):T$="YOU ACCUMULATED THE SUÍOF"+STR$(Z*10)+" GOLD PIECEÓ"
2105 ZZ=PEEK(PRS+5):T$=T$+"AND"+STR$(ZZ*50)+" ACRES OF LANÄ":Z=Z+ZZ*100
2110 T$=T$+"DURING YOUR CAREER; MAKINÇYOU A "
2115 ONSQR(Z/500)+1GOTO2120,2121,2122,2123,2124,2125,2126
2119 T$=T$+"EXTREMELY WEALTHY MAN®":GOTO2130
2120 T$=T$+"PENNILESS PAUPER®":GOTO2130
2121 T$=T$+"VERY POOR MAN®":GOTO2130
2122 T$=T$+"MAN OF AVERAGE WEALTH®":GOTO2130
2123 T$=T$+"SOMEWHAT PROPEROUS MAN®":GOTO2130
2124 T$=T$+"RATHER WEALTHY MAN®":GOTO2130
2125 T$=T$+"VERY WEALTHY MAN®":GOTO2130
2126 T$=T$+"EXTREMELY WEALTHY MAN®":GOTO2130
2130 X=2:Y=7:GOSUB8000
2200 Z=FNEX(PRS+4):T$="YOUR EXPLOITS ON THÅHIGH SEAS LEAVE YOÕWITH "
2210 T$=T$+"A REPUTATION AS Á"
2215 ONZ/4+2GOTO2220,2221,2222,2223,2224,2225
2219 T$=T$+"BRAVE; BOLD AND FEARLESS":GOTO2230
2220 T$=T$+"CAUTIOUS AND COWARDLY":GOTO2230
2221 T$=T$+"QUIET AND CAUTIOUS":GOTO2230
2222 T$=T$+"COMPETENT AND ACTIVE":GOTO2230
2223 T$=T$+"ACTIVE AND DARING":GOTO2230
2224 T$=T$+"DARING AND BOLD":GOTO2230
2225 T$=T$+"BOLD AND FEARLESS":GOTO2230
2230 T$=T$+"£CAPTAIN®":X=2:Y=11:GOSUB8000
2300 T$="YOU RETIRED FROM PRIVATEERINÇAT THE AGE OF"+STR$(YR+25)+"®"
2301 Z=PEEK(PRS+20):IFZ=0THENT$=T$+"YOU WERE UNABLE TÏFIND A BRIDE®":GOTO2310
2302 T$=T$+"YOUR BRIDE IS ":ONZGOSUB2304,2305,2306,2307:GOTO2310
2304 T$=T$+"A SHREWISH ANÄPESTERSOME CREATURE®":RETURN
2305 T$=T$+"A FRIENDLY ANÄATTRACTIVE WOMAN®":RETURN
2306 T$=T$+"A LOVELY ANÄCHEERFULL GIRL®":RETURN
2307 T$=T$+"A EXCITING ANÄBEAUTIFUL CREATURE®":RETURN
2310 Z=PEEK(PRS+6):IFZ=0THEN2320
2315 T$=T$+"YOU SUCCEEDED IN RESCUING"+STR$(Z)+"£MEMBER":IFZ>1THENT$=T$+"S"
2316 T$=T$+" OF YOUR FAMILY®"
2320 Z=PEEK(PRS+22):IFZ=0THEN2330
2325 T$=T$+"YOU WERE WOUNDED"+STR$(Z)+" TIME":IFZ>1THENT$=T$+"S"
2326 T$=T$+"£IN THE COURSE OF YOUR TRAVELS®"
2330 T$=T$+"YOU BECOME Á":IFHA<-.6THEN2340
2335 ONHA*3+3GOTO2340,2340,2340,2341,2342,2343,2344,2345
2339 T$=T$+"HAPPY AND FUN-LOVING":GOTO2350
2340 T$=T$+"SAD AND BITTER":GOTO2350
2341 T$=T$+"LONELY AND QUIET":GOTO2350
2342 T$=T$+"QUIET AND CONTENTED":GOTO2350
2343 T$=T$+"CONTENTED AND CHEERFUL":GOTO2350
2344 T$=T$+"CHEERFUL AND HAPPY":GOTO2350
2345 T$=T$+"HAPPY AND FUN-LOVING"
2350 T$=T$+" MAN®":X=2:Y=13:GOSUB8000
2500 RETURN:T$="YOUR CAREER PROSPECTSº"
2600 RETURN
2900 A=0:REM FIND CITY(A,C$) AT CURSOR
2910 POKEA0,NC:SYSWN+6:Z=CTY+CLN*PEEK(A0):IFZ=ATHEN2910
2940 A=Z
2950 C$="":FORI=A+12TOA+23:C$=C$+CHR$(PEEK(I)):NEXTI:IFMO=2THENRETURN
2955 Z=LEN(C$):IFRIGHT$(C$,1)=" "THENC$=LEFT$(C$,Z-1):GOTO2955
2960 RETURN
2980 T$=" PRESS TRIGGER TO CONTINUÅ":X=9:Y=21:GOSUB8000
2985 POKEA0,9:POKEA1,21:POKEA2,28:POKEA3,2:POKEA4,1:SYSWN:RETURN
2990 REM WAIT FOR TRIGGER
2991 IFPEEK(VB+14)=1THEN2991
2992 IFPEEK(VB+14)=0THEN2992
2993 POKEVB+16,0:RETURN
3000 Z=FNDP(PRS+2):T$="YOUR SHARE OF THE EXPEDITION&ÓPLUNDER IS"+STR$(Z*10)
3002 T$=T$+" GOLD PIECES®YOUR PERFORMANCE IN THIS VOYAGÅLEAVES YOU WITH "
3010 T$=T$+"A REPUTATION AÓA "
3015 ONZ^.7/40+2GOTO3020,3021,3022,3023,3024,3025
3019 T$=T$+"BRAVE; BOLD AND FEARLESS":GOTO3030
3020 T$=T$+"CAUTIOUS AND COWARDLY":GOTO3030
3021 T$=T$+"QUIET AND CAUTIOUS":GOTO3030
3022 T$=T$+"COMPETENT AND ACTIVE":GOTO3030
3023 T$=T$+"ACTIVE AND DARING":GOTO3030
3024 T$=T$+"DARING AND BOLD":GOTO3030
3025 T$=T$+"BOLD AND FEARLESS":GOTO3030
3030 T$=T$+" CAPTAIN®":X=2:Y=13:GOSUB8000:RETURN
5950 Z=-1:GOSUB8700:V=64:ZV=DTA:GOSUB7:V=65:ZV=MP:GOSUB7:V=66:ZV=SP:GOSUB7
5960 V=68:ZV=SO:GOSUB7:V=69:ZV=A0:GOSUB7:V=70:ZV=VB:GOSUB7:V=71:ZV=WN:GOSUB7
5970 V=67:ZV=PRS+128:GOSUB7:F=0:T=ZV/KP:L=.5:GOSUB12:V=72:ZV=CTY:GOSUB7
5980 V=73:ZV=DF:GOSUB7:V=74:ZV=PTY:GOSUB7:V=75:ZV=TR:GOSUB7:V=76:ZV=SA:GOSUB7
5990 V=77:ZV=PRS:GOSUB7:V=78:ZV=200*KP:GOSUB7:POKES+18,128:RETURN
7999 X=20-INT(LEN(T$)/2):Y=20
8000 C=0:GOTO8010:REM OPTIONS MENU
8005 C=1
8010 AA=PRS+131:POKEAA-1,0:POKEVB+15,1:T$="®"+T$:SYSWN+12:XL=PEEK(AA-2):DX=XL+2
8020 N=PEEK(AA-3):DY=N+1:CO=2+6*RND(0):GOSUB30:POKESP,176:Z=AA+PEEK(AA-1)
8030 POKE214,Y:PRINT"";:FORI=AA+NTOAA+1STEP-1:POKE211,X+1:IFZ=ITHENPRINT"";
8040 PRINTMID$(T$,PEEK(I)+2,PEEK(I-1)-PEEK(I))
8045 NEXTI:PRINT"";:IFCTHENRETURN
8050 Z=N-Z+AA:POKEVB+16,X*4+4:POKEVB+17,(Y+Z)*8+60:POKE53269,1
8060 C=INT(PEEK(VB+17)/8)-7-Y:IFC<ZTHENC=Z
8070 IFC>=NTHENC=N-1
8071 L=PEEK(216*KP+(Y+C)*40+X+42)AND15:POKEA0,X+1:POKEA1,Y+C+1:POKEA2,0:SYSWN+15
8072 GOSUB8090:IFPEEK(VB+14)THENPOKEVB+16,0:POKEA2,6*SGN(L):SYSWN+15:RETURN
8074 POKEA2,L:SYSWN+15:GOSUB8090:GOTO8060
8080 POKEVB+16,0:RETURN
8090 FORI=0TO20:IFPEEK(VB+14)THENI=999
8091 NEXTI:RETURN
8099 C1=0:C2=10
8100 REM PICTURE F$
8110 F=236:T=161:L=2:GOSUB10:F$=F$+".WIN":BF=232*KP:GOSUB17800
8115 Z=ABS(X)+Y*40+41:AX=216*KP+Z:Z=PEEK(648)*KP+Z
8120 POKEA0,ABS(X):POKEA1,Y:POKEA2,19-18*(X=-1):POKEA3,15:POKEA4,0:SYSWN
8130 POKE53282,C1:POKE53283,C2:F=232:T=228:L=6:GOSUB10:F=161:T=236:L=2:GOSUB10
8140 POKEA0,FNLB(Z):POKEA1,Z/KP:POKEA2,FNLB(AX):POKEA3,AX/KP:SYSWN+9
8150 IFX=-1THENX=0:Z=Z+18:ZZ=ZZ+18:AX=AX+18:GOTO8140
8160 RETURN
8590 GOSUB8600:T$=T$+"®":RETURN
8600 T$="":REM LOAD T$ FROM FILE F$
8610 OPEN8,8,8,F$+".TXT,S,R"
8620 INPUT#8,F$:IFF$="CO$"THENT$=T$+CO$:GOTO8620
8622 IFF$="C$"THENT$=T$+C$:GOTO8620
8625 Z=LEN(F$):IFF$="ZZ"THENT$=T$+MID$(STR$(ZZ),2)+" ":GOTO8620
8630 IFST=0ANDCO$<>"X"THENT$=T$+LEFT$(F$,Z-1)+CHR$(ASC(MID$(F$,Z))+128):GOTO8620
8640 T$=T$+F$:CLOSE8:RETURN
8700 Z=4*KP+ABS(Z)*100+2*(Z<0):REM MUSIC(Z)
8710 POKEA0,FNLB(Z):POKEA1,Z/KP:POKEA2,13:POKEA3,64
8720 Z=2:POKES+3,Z:POKES+10,Z:POKES+17,Z:POKEA4,9:POKEA5,10:SYSVB+9:RETURN
12000 REM SET UP
12010 POKE56578,PEEK(56578)OR3:POKE56576,(PEEK(56576)AND252)OR0
12020 S=54272:FORI=0TO23:POKES+I,0:NEXTI:POKES+24,15:POKES+15,10:POKES+18,129
12030 POKE53270,24:POKE53272,48+8:POKE648,204:SYSVB
12040 SX=53248:SY=53249:SC=53287:POKE53269,0:SP=PEEK(648)*KP+1016:RETURN
12500 POKEVB+13,0:POKE648,204:POKE53272,48+8:REM TEXT SCREEN
12510 PRINT"";:POKE53269,0:POKE53280,14:POKE53281,1:POKE53282,0:MO=1
12515 POKE53271,0:POKE53277,0:POKE53276,0
12520 POKE53270,24:POKE53265,27:RETURN
12900 REM
12910 F=0:T=VA/KP:L=.5:GOSUB12:T=(VA+160)/KP:L=4-160/KP:GOSUB12:POKE53269,0
12930 SD$=F$:F$=F$+".SID":BF=174*KP:GOSUB17800:RETURN
12950 REM
12960 POKEA0,0:POKEA1,174:SYSVAR-24:RETURN
12970 POKEA0,FNLB(Z*KP):POKEA1,Z:SYSVAR-24:RETURN
13000 VB=150*256:POKEVB+13,0:REM SET UP STANDARD SCREEN
13010 POKE56578,PEEK(56578)OR3:POKE56576,(PEEK(56576)AND252)OR3
13020 POKE53265,27
13030 POKE53270,8:POKE53272,21:POKE648,4:SYSVB+3:POKEVB+15,0
13040 POKE53269,0:POKE53280,0:POKE53281,6:PRINT"":RETURN
17800 RR=0:REM LOAD FILE
17805 SYSVB+3:GOSUB41:GOSUB19520
17806 POKEA1,FNLB(BF):POKEA2,BF/256
17810 POKEA0,0:POKEA4,213:POKEA5,255:SYSKERNEL
17820 GOSUB40:SYSVB:RETURN
17900 RR=255:GOTO17805
19000 DEFFNDP(X)=PEEK(X)+256*PEEK(X+1)
19001 DEFFNLB(X)=(X-32768)AND255:DEFFNEX(X)=PEEK(X)+256*(PEEK(X)>127)
19002 DEFFNRG(X)=-X*(X>=0ANDX<256)-255*(X>255)
19003 A0=165:A1=A0+1:A2=A1+1:A3=A2+1:A4=A3+1:A5=A4+1:A6=A5+1:KP=256:KK=KP*KP
19004 K=149*KP:SB=K+41:MVB=K+70:BMVB=K+103:KERNEL=K+142:S64K=K+29:M64K=K+20
19010 VAR=159*KP:RETURN
19520 FORI=0TOLEN(F$)-1:POKE830+I,ASC(MID$(F$,I+1,1)):NEXTI
19526 POKEA0,0:POKEA1,8:POKEA2,1:POKEA4,186:POKEA5,255:SYSKE
19528 POKEA0,LEN(F$):POKEA1,62:POKEA2,3:POKEA4,189:POKEA5,255:SYSKE:RETURN
21900 SYSVB+3:PRINT"LOAD "CHR$(34);F$;CHR$(34)",8,1RUN"
21910 PRINT,"ONE MOMENT PLEASE...";
21920 GOSUB41:POKE631,13:POKE632,13:POKE198,3:NEW
22000 REM GET DISK ID
22060 RR=PEEK(53269):POKE53269,0:OPEN15,8,15,"I0:":OPEN2,8,2,"#1"
22070 PRINT#15,"U1";2;0;18;0:PRINT#15,"B-P";2;163:GET#2,I$:CLOSE2:CLOSE15
22090 POKE53269,RR:RETURN
